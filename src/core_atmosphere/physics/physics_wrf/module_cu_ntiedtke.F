!===================================================================================================

module module_cu_ntiedtke

! USE ccpp_kinds, ONLY: kind_phys

  use mpas_atmphys_constants, only: g=>gravity, rd=>r_d, rv=>r_v

  use cu_ntiedtke_mpas
  use cu_ntiedtke2, only: cu_ntiedtke_run

  implicit none
  PUBLIC :: cu_ntiedtke

contains
!-----------------------------------------------------------------------
     subroutine cu_ntiedtke(                                    &
                 dt,itimestep,stepcu                            &
                ,raincv,pratec,qfx,hfx                          &
                ,u3d,v3d,w3d,t3d,qv3d,qc3d,qi3d,pi3d,rho3d      &
                ,qvften,thften                                  &
                ,dz8w,pcps,p8w,xland,cu_act_flag,dx             &
                ,ids,ide, jds,jde, kds,kde                      &
                ,ims,ime, jms,jme, kms,kme                      &
                ,its,ite, jts,jte, kts,kte                      &
                ,rthcuten,rqvcuten,rqccuten,rqicuten            &
                ,rucuten, rvcuten                               &
                ,f_qv    ,f_qc    ,f_qr    ,f_qi    ,f_qs       &
                                                                )
!-------------------------------------------------------------------
      implicit none
!-------------------------------------------------------------------
!-- u3d         3d u-velocity interpolated to theta points (m/s)
!-- v3d         3d v-velocity interpolated to theta points (m/s)
!-- th3d        3d potential temperature (k)
!-- t3d         temperature (k)
!-- qv3d        3d water vapor mixing ratio (kg/kg)
!-- qc3d        3d cloud mixing ratio (kg/kg)
!-- qi3d        3d ice mixing ratio (kg/kg)
!-- rho3d       3d air density (kg/m^3)
!-- p8w         3d hydrostatic pressure at full levels (pa)
!-- pcps        3d hydrostatic pressure at half levels (pa)
!-- pi3d        3d exner function (dimensionless)
!-- qvften      3d total advective + PBL moisture tendency (kg kg-1 s-1)
!-- thften      3d total advective + PBL + radiative temperature tendency (k s-1)
!-- rthcuten    theta tendency due to 
!               cumulus scheme precipitation (k/s)
!-- rucuten     u wind tendency due to 
!               cumulus scheme precipitation (k/s)
!-- rvcuten     v wind tendency due to 
!               cumulus scheme precipitation (k/s)
!-- rqvcuten    qv tendency due to 
!               cumulus scheme precipitation (kg/kg/s)
!-- rqccuten    qc tendency due to 
!               cumulus scheme precipitation (kg/kg/s)
!-- rqicuten    qi tendency due to 
!               cumulus scheme precipitation (kg/kg/s)
!-- rainc       accumulated total cumulus scheme precipitation (mm)
!-- raincv      cumulus scheme precipitation (mm)
!-- pratec      precipitiation rate from cumulus scheme (mm/s)
!-- dz8w        dz between full levels (m)
!-- qfx         upward moisture flux at the surface (kg/m^2/s)
!-- hfx         upward heat flux at the surface (w/m^2) 
!-- dt          time step (s)
!-- ids         start index for i in domain
!-- ide         end index for i in domain
!-- jds         start index for j in domain
!-- jde         end index for j in domain
!-- kds         start index for k in domain
!-- kde         end index for k in domain
!-- ims         start index for i in memory
!-- ime         end index for i in memory
!-- jms         start index for j in memory
!-- jme         end index for j in memory
!-- kms         start index for k in memory
!-- kme         end index for k in memory
!-- its         start index for i in tile
!-- ite         end index for i in tile
!-- jts         start index for j in tile
!-- jte         end index for j in tile
!-- kts         start index for k in tile
!-- kte         end index for k in tile
!-------------------------------------------------------------------

      integer, intent(in) ::            ids,ide, jds,jde, kds,kde,      &
                                        ims,ime, jms,jme, kms,kme,      &
                                        its,ite, jts,jte, kts,kte,      &
                                        itimestep,                      &
                                        stepcu

      real,    intent(in) ::                                            &
                                        dt
      real,    dimension(ims:ime, jms:jme), intent(in) ::               &
                                        dx

      real,    dimension(ims:ime, jms:jme), intent(in) ::               &
                                        xland

      real,    dimension(ims:ime, jms:jme), intent(inout) ::            &
                                        raincv, pratec

      logical, dimension(ims:ime,jms:jme), intent(inout) ::             &
                                        cu_act_flag

      real,    dimension(ims:ime, kms:kme, jms:jme), intent(in) ::      &
                                        dz8w,                           &
                                        pcps,                           &
                                        p8w,                            &
                                        pi3d,                           &
                                        qc3d,                           &
                                        qvften,                         &
                                        thften,                         &
                                        qi3d,                           &
                                        qv3d,                           &
                                        rho3d,                          &
                                        t3d,                            &
                                        u3d,                            &
                                        v3d,                            &
                                        w3d

      real,    dimension(ims:ime, jms:jme), intent(in) ::               &
                                        qfx,                            &
                                        hfx                            

!--------------------------- optional vars ----------------------------

      real, dimension(ims:ime, kms:kme, jms:jme),                       &
               optional, intent(out) ::                                 &
                                        rqccuten,                       &
                                        rqicuten,                       &
                                        rqvcuten,                       &
                                        rthcuten,                       &
                                        rucuten,                        &
                                        rvcuten
!
! flags relating to the optional tendency arrays declared above
! models that carry the optional tendencies will provdide the
! optional arguments at compile time; these flags all the model
! to determine at run-time whether a particular tracer is in
! use or not.
!
     logical, optional ::                                    &
                                                   f_qv      &
                                                  ,f_qc      &
                                                  ,f_qr      &
                                                  ,f_qi      &
                                                  ,f_qs

!--------------------------- local vars ------------------------------
      real      ::                                                      &
                                        delt,                           &
                                        rdelt

      real     , dimension(its:ite) ::                                  &
                                        evap,                           &
                                        heatflux,                       &
                                        dx2d

      integer  , dimension(its:ite) ::  slimsk

      real     , dimension(its:ite, kts:kte+1) ::                       &
                                        dzi,                            &
                                         w

      real     , dimension(its:ite, kts:kte) ::                         &
                                        rho

      integer ::                                                        &
                                        i,                              &
                                        im,                             &
                                        j,                              &
                                        k,                              &
                                        km,                             &
                                        kp,                             &
                                        kx,                             &
                                        kx1

!-------other local variables----
      integer                      :: zz, pp
!
!------- new local variables ----
!
      real     , dimension(its:ite, kts:kte) ::                         &
                                        pap,                            &
                                        pqv,                            &
                                        pqc,                            &
                                        pqi,                            &
                                       pqvf,                            &
                                        ptf,                            &
                                         pt,                            &
                                         pu,                            &
                                         pv

      real     , dimension(its:ite, kts:kte) ::                         &
                                       pomg,                            &
                                       pgeo,                            &
                                         pq,                            &
                                      pqsat

      real     , dimension(its:ite, kts:kte+1) ::                       &
                                       paph,                            &
                                      pgeoh

      real     , dimension(its:ite, kts:kte) ::                         &
                                        ztt,                            &
                                        zqq

      real     , dimension(its:ite) ::                                  &
                                     zprecc
!
! more local arrays for cu which are not output out of the routine
!
      real     , dimension(its:ite, kts:kte) ::                         &
                                       pcte ,                           &
                                       pvom ,                           &
                                       pvol

      real     , dimension(its:ite) ::                                  &
                                      prsfc,                            &
                                      pssfc,                            &
                                      zrain

      real     , dimension(its:ite, kts:kte) ::                         &
                                         ztu ,                          &
                                         zqu ,                          &
                                         zlu ,                          &
                                       zlude ,                          &
                                        zmfu ,                          &
                                        zmfd

      integer  , dimension(its:ite) ::                                  &
                                       icbot,                           &
                                       ictop,                           &
                                       ktype

      logical  , dimension(its:ite) :: locum

      real     :: zew,zqs,zcor
!
!     set flag for a convection timestep
!
      do j=jts,jte
         do i=its,ite
            cu_act_flag(i,j)=.true.
         enddo
      enddo

      im=ite-its+1
      kx=kte-kts+1
      kx1=kx+1
      delt=dt*stepcu
      rdelt=1./delt

!  begin first j loop

   do j=jts,jte

      do k=kts,kte+1
        do i=its,ite
          w(i,k)=w3d(i,k,j)
          dzi(i,k)=dz8w(i,k,j)
        enddo
      enddo
 
      do k=kts,kte
        do i=its,ite
          rho(i,k)=rho3d(i,k,j)
        enddo
      enddo

      do i=its,ite
        slimsk(i)=int(abs(xland(i,j)-2.))
      enddo

      do i=its,ite
         dx2d(i) = dx(i,j)
      enddo

      pp = 0
      do k=kts,kte
        zz = kte-pp
        do i=its,ite
          pu(i,zz)=u3d(i,k,j)
          pv(i,zz)=v3d(i,k,j)
          pt(i,zz)=t3d(i,k,j)
          pqv(i,zz)=qv3d(i,k,j)
          pqvf(i,zz)=qvften(i,k,j)
          ptf(i,zz)=thften(i,k,j)
          pqc(i,zz)=qc3d(i,k,j)
          pqi(i,zz)=qi3d(i,k,j)
          pap(i,zz)=pcps(i,k,j)
        enddo
        pp = pp + 1
      enddo

      pp = 0
      do k=kts,kte+1
        zz = kte+1-pp
        do i=its,ite
          paph(i,zz) = p8w(i,k,j) 
        enddo
        pp = pp + 1
      enddo

      do i=its,ite
        evap(i) = qfx(i,j)
        heatflux(i) = hfx(i,j)
      enddo

   enddo ! first j loop

   call ntiedtke_mpas_timestep_init(                            &
                 its,ite, kts,kte, itimestep                    &
                ,w,pt,pqv,rho,dzi,pqvf,ptf,pap                  &
                ,ztt,zqq                                        &
                ,pomg,pq,pqsat,pgeo,pgeoh                       &
                ,zrain,locum,prsfc,pssfc                        &
                ,pcte,pvom,pvol                                 &
                ,g,rv,rd                                        &
                                                                )
!
!     call 'cu_ntiedtke_run'(master-routine for cumulus parameterization)
!          (originally cumastrn)
!
   call cu_ntiedtke_run                                                        &
     &    (klon=im,     klev=kx,        klevp1=kx1,  klevm1=kx-1, pten=pt,     &
     &     pqen=pq,     puen=pu,        pven=pv,     pverv=pomg,  pqsen=pqsat, &
     &     pqhfl=evap,  ztmst=delt,     pap=pap,     paph=paph,   pgeo=pgeo,   &
     &     ptte=ptf,    pqte=pqvf,      pvom=pvom,   pvol=pvol,                &
     &     prsfc=prsfc, pssfc=pssfc,    ldcum=locum,                           &
     &     ktype=ktype, kcbot=icbot,    kctop=ictop, ptu=ztu,     pqu=zqu,     &
     &     plu=zlu,     plude=zlude,    pmfu=zmfu,   pmfd=zmfd,   prain=zrain, &
     &     pcte=pcte,   phhfl=heatflux, lndj=slimsk, zgeoh=pgeoh, dx=dx2d)

   call ntiedtke_mpas_timestep_final(                           &
                 its,ite, kts,kte, delt                         &
                ,pcte,ptf,pqvf,prsfc,pssfc                      &
                ,pvom,pvol,ztt,zqq                              &
                ,pqc,pqi,pt,pq,pqv,pu,pv                        &
                ,zprecc                                         &
                                                                )
!
!     compute tendencies for model variables
!
   do j=jts,jte

      do i=its,ite
         raincv(i,j)=zprecc(i)/stepcu
         pratec(i,j)=zprecc(i)/(stepcu * dt)
      enddo

      pp = 0
      do k=kts,kte
        zz = kte-pp
        do i=its,ite
          rthcuten(i,k,j)=(pt(i,zz)-t3d(i,k,j))/pi3d(i,k,j)*rdelt
          rqvcuten(i,k,j)=(pqv(i,zz)-qv3d(i,k,j))*rdelt
          rucuten(i,k,j) =(pu(i,zz)-u3d(i,k,j))*rdelt
          rvcuten(i,k,j) =(pv(i,zz)-v3d(i,k,j))*rdelt
        enddo
        pp = pp + 1
      enddo

      if(present(rqccuten))then
        if ( f_qc ) then
          pp = 0
          do k=kts,kte
            zz = kte-pp
            do i=its,ite
              rqccuten(i,k,j)=(pqc(i,zz)-qc3d(i,k,j))*rdelt
            enddo
            pp = pp + 1
          enddo
        endif
      endif

      if(present(rqicuten))then
        if ( f_qi ) then
          pp = 0
          do k=kts,kte
            zz = kte-pp
            do i=its,ite
              rqicuten(i,k,j)=(pqi(i,zz)-qi3d(i,k,j))*rdelt
            enddo
            pp = pp + 1
          enddo
        endif
      endif

   enddo
!  end of second j loop

   end subroutine cu_ntiedtke

!====================================================================

end module module_cu_ntiedtke
